//===== eAthena Script ======================================= 
//= Einbroch Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= eAthena Revision 3000+
//===== Description: ========================================= 
//= Einbroch Airport Staff
//===== Additional Comments: ================================= 
//= Converted by Dr.Evil, 1.0b more typos and credits [Lupus]
//= 1.1 Fixed wrong check, added extra condition [Justin84]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Fixed syntax errors for duplicate [KarLaeda]
//= 1.4 Removed use of goto. [L0ne_W0lf]
//===== Translated into Russian: =============================
//= 0.1 Исправлены небольшие ошибки [mad_max] (MistiQ Group © 2007)
//= Коррекция [Vorp]
//============================================================ 


airport,126,43,4	script	Airport Staff#01::AirportE	90,{
	mes "[Сотрудник Аэропорта]";
	mes "Добро пожаловать в Аэропорт.";
	mes "Чем могу вам помочь?";
	next;
	if (select("Лететь на Дирижабле:Отмена") == 1) {
		mes "[Сотрудник Аэропорта]";
		mes "Полет на Дирижабле стоит";
		mes "1,200 zeny, но если у вас есть";
		mes "Бесплатный Билет на Дирижабль"; 
		mes "то платить не нужно. Полетите";
		mes "на Дирижабле?";
		next;
		if (select("Да:Нет") == 1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "airport",148,51;
				close;
			}
			if(Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				close;
			}
			mes "[Сотрудник Аэропорта]";
			mes "У вас недостаточно zeny.";
			close;
		}
	}
	mes "[Сотрудник Аэропорта]";
	mes "Спасибо и приятного";
	mes "вам дня.";
	close;
}

airport,143,43,4	duplicate(AirportE)	Airport Staff#02	90
airport,156,43,4	duplicate(AirportE)	Airport Staff#03	90

airport,126,51,4	script	Airport Staff#04::AirportE2	90,{
	mes "[Сотрудник Аэропорта]";
	mes "Добро пожаловать~";
	mes "Пожалуйста пройдите сюда,";
	mes "чтобы сесть на Дирижабль.";
	next;
	if (select("Выход к Главному Терминалу:Отмена") == 1) {
		mes "[Сотрудник Аэропорта]";
		mes "Если вы покините главный";
		mes "терминал, то вам придется";
		mes "снова заплатить, чтобы попасть";
		mes "на борт Дирижабля.";
		mes "Вы уверенны,";
		mes "что хотите выйти?";
		next;
		if (select("Да:Нет") == 1) {
			warp "airport",142,40;
			close;
		}
	}
	mes "[Сотрудник Аэропорта]";
	mes "Ну что ж, благодарю";
	mes "вас за сотрудничество";
	mes "и надеюсь, что перелёт";
	mes "вам понравился~";
	close;
}

airport,143,51,4	duplicate(AirportE2)	Airport Staff#05	90
airport,156,51,4	duplicate(AirportE2)	Airport Staff#06	90

einbroch,94,267,4	script	Airship Staff#07	91,{
	mes "[Сотрудник Аэропорта]";
	mes "Добро пожаловать в аэропорт";
	mes "города Einbroch. Пожалуйста";
	mes "пройдите в эту дверь для посадки";
	mes "на Дирижабль следующий с остановками";
	mes "в городах Lighthalzen и Juno входящих";
	mes "в Республику Schwarzwald.";
	next;
	mes "[Сотрудник Аэропорта]";
	mes "В противном случае, если Einbroch";
	mes "пункт вашего назначения,";
	mes "спуститесь вниз по лестнице";
	mes "и попросите Встречающий Персонал";
	mes "проводить вас к главному терминалу.";
	mes "Спасибо вам, наслаждайтесь путешествиями.";
	close;
}
