//===== eAthena Script ======================================= 
//= Einbroch Guides
//===== By: ================================================== 
//= Muad_dib; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Guides for the City of Einbroch
//===== Additional Comments: ================================= 
//= 1.0b missing tabs, fixed Guide coords [Lupus]
//= 1.1 added 1 more Guide NPC, thanx to Justin84 [Lupus]
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.4 Removed Duplicates [Silent]
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//===== Translated into Russian: =============================
//= перевод Shadow Smail rater@yandex.ru
//============================================================

einbroch,72,202,4	script	Guide#ein::EinGuide	852,{
	cutin "ein_soldier",2;
	mes "[Einbroch Guide]";
	mes "Добро пожаловать";
	mes "в Einbroch,";
	mes "Город Стали!";
	mes "Если что-то интересует,";
	mes "спроси у меня.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Городской путеводитель.:Убрать отметки с мини-карты.:Справка.:Выйти.")) {
		case 1:
			mes "[Einbroch Guide]";
			mes "Выбери свой";
			mes "пункт назначения";
			mes "в следующем меню.";
			if (.@compass_check == 0) {
				mes "Хочешь, чтобы я";
				mes "отметил здания";
				mes "на твоей карте?";
				next;
				if (select("Да.:Нет.") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				switch(select("^FF0000Аэропорт^000000:Вокзал:Завод:Рыночная площадь:Отель:Оружейный магазин:Лаборатория:Гильдия Кузнецов:Башня Эйнброча:Отмена")) {
				case 1:
					mes "[Einbroch Guide]";
					mes "^FF0000Airport^000000 у нас";
					mes "находится в северо-западной";
					mes "части города. Там ты можешь";
					mes "посмотреть на гордость нашего города - Дирижабль. Конечно, чтобы покататься на нем, тебе нужно заплатить.";
					if (.@compass_check)
						viewpoint 1,63,228,2,0xFF0000;
					break;
				case 2:
					mes "[Einbroch Guide]";
					mes "Наш Вокзал ";
					mes "расположен в северо-восточной";
					mes "части города. Поезда";
					mes "до соседнего города,";
					mes "Einbech'а, ходят весь день,";
					mes "ежедневно.";
					if (.@compass_check)
						viewpoint 1,236,279,3,0xFF00FF;
					break;
				case 3:
					mes "[Einbroch Guide]";
					mes "Завод, главный";
					mes "источник дохода";
					mes "Einbroch'а, находится";
					mes "в южной части города.";
					if (.@compass_check)
						viewpoint 1,158,78,4,0xFF00FF;
					break;
				case 4:
					mes "[Einbroch Guide]";
					mes "Plaza, наш";
					mes "любимый шопинговый центр,";
					mes "находится к востоку";
					mes "от центра города.";
					if (.@compass_check)
						viewpoint 1,232,190,5,0xFF00FF;
					break;
				case 5:
					mes "[Einbroch Guide]";
					mes "Если ты приехал";
					mes "к нам надолго, остановись в нашем";
					mes "пятизвездочном отеле! Он";
					mes "находится на востоке от Plaza.";
					mes "Поверь, тебе там понравится!";
					if (.@compass_check)
						viewpoint 1,260,201,6,0x00FF00;
					break;
				case 6:
					mes "[Einbroch Guide]";
					mes "Оружейный магазин";
					mes "находится к северу от";
					mes "Plaza. Там ты можешь";
					mes "приобрести любое оружие,";
					mes "какое понравится.";
					if (.@compass_check)
						viewpoint 1,215,221,7,0x00FF00;
					break;
				case 7:
					mes "[Einbroch Guide]";
					mes "Лаборатория принадлежит";
					mes "Заводу и находится";
					mes "в юго-западном";
					mes "секторе города.";
					if (.@compass_check)
						viewpoint 1,36,49,8,0x00FF;
					break;
				case 8:
					mes "[Einbroch Guide]";
					mes "Гильдия Кузнецов";
					mes "расположена на юго-востоке.";
					mes "Они предлагают услуги";
					mes "по улучшению экипировки.";
					mes "Зайди туда, проверь.";
					if (.@compass_check)
						viewpoint 1,244,90,9,0x00FF00;
					break;
				case 9:
					mes "[Einbroch Guide]";
					mes "Башня Einbroch'а";
					mes "находится в самом";
					mes "центре города.";
					mes "С крыши башни виден";
					mes "весь Einbroch.";
					if (.@compass_check)
						viewpoint 1,174,195,10,0xFFFF00;
					break;
				case 10:
					mes "[Einbech Guide]";
					mes "Если хочешь чтобы я убрал";
					mes "отметки с твоей карты,";
					mes "выбери сооответствующий";
					mes "пункт в меню.";
					mes "Удачи.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,63,228,2,0xFF0000;
			viewpoint 2,236,279,3,0xFF00FF;
			viewpoint 2,158,78,4,0xFF00FF;
			viewpoint 2,232,190,5,0xFF00FF;
			viewpoint 2,260,201,6,0x00FF00;
			viewpoint 2,215,221,7,0x00FF00;
			viewpoint 2,36,49,8,0x00FF00;
			viewpoint 2,244,90,9,0x00FF00;
			viewpoint 2,174,195,10,0xFFFF00;
			set .@compass_check,0;
			mes "[Einbroch Guide]";
			mes "Ок, я убрал";
			mes "все отметки с твоей карты.";
			mes "Но если тебе потом";
			mes "понадобится помощь,";
			mes "не стесняйся, подойди ко мне";
			mes "или к другим Гидам.";
			break;
		case 3:
			mes "[Einbroch Guide]";
			mes "Благодаря технологиям";
			mes "Республики Schwaltzvalt,";
			mes "мы улучшили нашу";
			mes "информационную систему,";
			mes "что позволяет нам отмечать крестиками";
			mes "здания на твоей карте.";
			next;
			mes "[Einbroch Guide]";
			mes "Твоя карта расположена";
			mes "В верхнем правом углу";
			mes "экрана. Если ее там нет,";
			mes "жмакай Ctrl + Tab";
			mes "или ''Map'' в твоем Basic Info Window.";
			next;
			mes "[Einbroch Guide]";
			mes "Чтобы отдалять и приближать";
			mes " карту, тыкай ''+'' and ''-'',";
			mes "которые находятся на ней.";
			mes "Я надеюсь тебе понравилось";
			mes "у нас в городе.";
			mes "Приезжай еще!";
			break;
		case 4:
			mes "[Einbroch Guide]";
			mes "Я надеюсь, тебе понравилось";
			mes "здесь у нас!";
			mes "Удачи, приезжай еще!";
			close2;
			set .@loop1,0;
		}
	}
	cutin "ein_soldier",255;
	end;
}

einbroch,155,43,4	duplicate(EinGuide)	Guide#2ein	852

einbroch,162,317,4	duplicate(EinGuide)	Guide#3ein	852
