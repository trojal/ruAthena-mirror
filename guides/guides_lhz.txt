//===== eAthena Script ======================================= 
//= Lighthalzen Guides
//===== By: ================================================== 
//= MasterOfMuppets; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Guides for the City of Lighthalzen
//===== Additional Comments: ================================= 
//= 1.0 First Version [MasterOfMuppets]
//= 1.1 Removed Duplicates [Silent]
//= 1.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//===== Translated into Russian: =============================
//= Адаптация [Vorp]
//============================================================

lighthalzen,207,310,5	script	Guide#lhz::LhzGuide	852,{
	cutin "ein_soldier",2;
	mes "[Солдат Лайтхальзена]";
	mes "Добро пожаловать в Лайтхальзен.";
	mes "Город-государство корпорации.";
	mes "Если Вам нужна помощь";
	mes "в поиске зданий, не стесняйтесь";
	mes "можете спрашивать меня";
	mes "когда угодно и что угодно.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Городской путеводитель:Убрать отметки с мини-карты:Справка:Отмена")) {
		case 1:
			mes "[Солдат Лайтхальзена]";
			mes "Имейте в виду, что я";
			mes "отвечаю за информацию";
			mes "о Западном районе,";
			mes "Лайтхальзена. А теперь,";
			mes "скажите пожалуйста";
			mes "о чём Вы хотите узнать.";
			if (.@compass_check == 0) {
				next;
				mes "[Солдат Лайтхальзена]";
				mes "Но перед этим,";
				mes "не хотите ли Вы,";
				mes "чтобы я сделал";
				mes "пометки на Вашей карте?";
				next;
				if (select("Да.:Нет.") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				switch(select("^FF0000Корпорация Рекенбер^000000:Вокзал:Полицейский участок:Банк:Отель:Аэропорт:Торговая Гильдия:Ювелирный магазин:Оружейный магазин:Универмаг:Отмена")) {
				case 1:
					mes "[Солдат Лайтхальзена]";
					mes "Корпорация Рекенбер";
					mes "Самая большая компания";
					mes "в Шварцвальдской республике.";
					mes "Расположена на северо-востоке";
					mes "Лайтхальзена. Вы не сможете ";
					mes "пропустить такое большое здание.";
					if (.@compass_check)
						viewpoint 1,102,247,2,0xFF0000;
					break;
				case 2:
					mes "[Солдат Лайтхальзена]";
					mes "Железнодорожный вокзал";
					mes "расположен в центре";
					mes "города, там у нас";
					mes "прямая дорога до Эйнброча.";
					if (.@compass_check)
						viewpoint 1,233,164,3,0xFF00FF;
					break;
				case 3:
					mes "[Солдат Лайтхальзена]";
					mes "Наш полицейский участок";
					mes "находится севернее центра.";
					mes "Пожалуйста не сомневайтесь";
					mes "и сразу говорите нам о";
					mes "подозрительных действиях";
					mes "или людях...";
					if (.@compass_check)
						viewpoint 1,236,276,4,0x99FFFFF;
					break;
				case 4:
					mes "[Солдат Лайтхальзена]";
					mes "Банк расположен";
					mes "напротив Полицейского";
					mes "участка Лайтхальзена,";
					mes "что, как мне кажется,";
					mes "довольно неплохая идея.";
					if (.@compass_check)
						viewpoint 1,198,257,5,0x0000FF;
					break;
				case 5:
					mes "[Солдат Лайтхальзена]";
					mes "Отель находится в";
					mes "центре южной площади.";
					mes "Благодаря хорошему сервису";
					mes "и богатому набору услуг,";
					mes "он очень популярен.";
					if (.@compass_check)
						viewpoint 1,159,133,6,0x00FF00;
					break;
				case 6:
					mes "[Солдат Лайтхальзена]";
					mes "Аэропорт на западном";
					mes "конце главной улицы.";
					mes "Вы можете перемещаться";
					mes "по городам Шварцвальдской";
					mes "республики на воздушных кораблях.";
					if (.@compass_check)
						viewpoint 1,267,75,7,0x00FF00;
					break;
				case 7:
					mes "[Солдат Лайтхальзена]";
					mes "Гильдия торговцев";
					mes "расположена в юго-";
					mes "западной части города";
					if (.@compass_check)
						viewpoint 1,74,53,8,0xFF99FF;
					break;
				case 8:
					mes "[Солдат Лайтхальзена]";
					mes "Магазин драгоценностей";
					mes "находится на западе";
					mes "южной площади.";
					if (.@compass_check)
						viewpoint 1,93,110,9,0xFF9900;
					break;
				case 9:
					mes "[Солдат Лайтхальзена]";
					mes "Арсенал располжен";
					mes "в конце Главной";
					mes "улицы. Туда стоит";
					mes "заглянуть, если Вы серьёзно ";
					mes "собираетесь в поход или";
					mes "в большое путешествие.";
					if (.@compass_check)
						viewpoint 1,196,46,10,0x330033;
					break;
				case 10:
					mes "[Солдат Лайтхальзена]";
					mes "Универмаг Вы найдёте";
					mes "в центре Лайтхальзена";
					mes "это самое большое и";
					mes "наиболее удобное место";
					mes "для покупок во всём мире.";
					if (.@compass_check)
						viewpoint 1,199,163,11,0xFFFF00;
					break;
				case 11:
					mes "[Солдат Лайтхальзена]";
					mes "Если вы больше не";
					mes "нуждаетесь в пометках";
					mes "на мини-карте,";
					mes "то скажите мне";
					mes "и я их уберу.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,102,247,2,0xFF0000;
			viewpoint 2,233,164,3,0xFF00FF;
			viewpoint 2,236,276,4,0x99FFFF;
			viewpoint 2,198,257,5,0x0000FF;
			viewpoint 2,159,133,6,0x00FF00;
			viewpoint 2,267,75,7,0x00FF00;
			viewpoint 2,74,53,8,0xFF99FF;
			viewpoint 2,93,110,9,0xFF9900;
			viewpoint 2,196,46,10,0x330033;
			viewpoint 2,199,163,11,0xFFFF00;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Солдат Лайтхальзена]";
			mes "Достижения в науке и магии,";
			mes "позволили улучшить нашу";
			mes "информационную систему.";
			mes "Теперь мы можем отмечать";
			mes "на вашей мини-карте всё,";
			mes "для удобной навигации.";
			next;
		  	mes "[Солдат Лайтхальзена]";
			mes "Ваша мини-карта расположена";
			mes "в верхнем правом углу экрана.";
			mes "Если вы не видите её, нажмите";
			mes "сочетание клавиш Ctrl + Tab или";
			mes "кнопку ''Карта'' в окне Основной Информации.";
			next;
		  	mes "[Солдат Лайтхальзена]";
			mes "На вашей мини-карте,";
			mes "щелкните на символах ''+'' и";
			mes "''-'', чтобы изменить масштаб";
			mes "на Мини-карте. Мы надеемся,";
			mes "что вам понравится";
			mes "наш город Лайтхальзен.";
			break;
		case 4:
			mes "[Солдат Лайтхальзена]";
			mes "Лайтхальзен разделён";
			mes "на восточную и западную";
			mes "части железной дорогой,";
			mes "которая проходит прямо по середине.";
			mes "Стража всегда на посту";
			mes "и следит за порядком.";
			next;
			mes "[Солдат Лайтхальзена]";
			mes "Пожалуйста не сомневайтесь";
			mes "и сразу говорите нам о";
			mes "подозрительных действиях";
			mes "или людях. Надеюсь Вам";
			mes "понравится наш город, путник.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "ein_soldier",255;
	end;
}

lighthalzen,220,311,3	duplicate(LhzGuide)	Guide#2lhz	852

lighthalzen,154,100,5	duplicate(LhzGuide)	Guide#3lhz	852

lighthalzen,307,224,3	duplicate(LhzGuide)	Guide#4lhz	852
