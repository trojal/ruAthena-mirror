//===== eAthena Script ======================================= 
//= Veins Guide
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [iRO-Based]
//= Veins City guide
//===== Additional Comments: ================================= 
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Added second Veins guide NPC. [L0ne_W0lf]
//= 1.2 Fixed a few small typos. [L0ne_W0lf]
//===== Translated into Russian: =============================
//= Адаптация [Vorp]
//============================================================ 

veins,210,345,5	script	Veins Guide#1::ve_guide	934,{
	mes "[Veins Guide]";
	mes "Пустынный город Veins приветсвует искателей приключений, ищущих убежище от суровых песчанных бурь.";
	mes "Если ты впервые решил вопсользоваться услугами гида, то почему бы вам не выбрать 'Заметки'...";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("Деревенский путеводитель:Убрать все отметки с мини-карты:Справка:Отмена")) {
		case 1:
			mes "[Veins Guide]";
			mes "Я могу рассказать вам о любом здании, которое расположено здесь, в Vein.";
			mes "Так куда ты хочешь пойти?";
			if (.@compass_check == 0) {
				mes "Хотите, что бы я";
				mes "сделал отметки на";
				mes "вашей мини-карте?";
				next;
				if (select("Да:Нет") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("Храм:Гостиница:Оружейный магазин:Товарная лавка:Аэропорт:Таверна:Институт Геологии:Отмена")) {
				case 1:
					mes "[Veins Guide]";
					mes "Наш храм расположен к северу,";
					mes "и всегда переполнен искренними последователями богини Freya.";
					if (.@compass_check == 1) viewpoint 1,196,258,1,0xFF0000;
					break;
				case 2:
					mes "[Veins Guide]";
					mes "Если Вы устали, можете остановится в Гостинице.";
					mes "Здание с лева от меня - Гостиница Veins.";
					if (.@compass_check == 1) viewpoint 1,128,266,2,0xFF00FF;
					break;
				case 3:
					mes "[Veins Guide]";
					mes "Да, вы должны защищать себя от опасностей самостоятельно.";
					mes "Купите высококачественное оружие по доступным ценам.";
					mes "Оружейный Магазин Vein'а sрасположен на западе.";
					if (.@compass_check == 1) viewpoint 1,150,175,3,0x99FFFF;
					break;
				case 4:
					mes "[Veins Guide]";
					mes "Вы взяли с собой достаточно необходимого для путешествий?";
					mes "Если нет, то я предлагаю вам посетить и посмотреть предложения нашего магазина.";
					if (.@compass_check == 1) viewpoint 1,230,161,4,0x0000FF;
					break;
				case 5:
					mes "[Veins Guide]";
					mes "Имейте в виду, что в Viens заходят лишь грузовые корабли.";
					if (.@compass_check == 1) viewpoint 1,273,285,5,0x00FF00;
					break;
				case 6:
					mes "[Veins Guide]";
					mes "Если вы хотите подружится";
					mes "с горожанами, то я советую вам";
					mes "посетить таверну и угостить всех,";
					mes "это на западе.";
					if (.@compass_check == 1) viewpoint 1,150,217,6,0x00FF00;
					break;
				case 7:
					mes "[Veins Guide]";
					mes "Вы заинтересованы в изучении геологии?";
					mes "Тогда вам лучше пойти проверить";
					mes "Геологический Научно-исследовательский институт";
					mes "на 2ом этаже, в оружейном магазине.";
					if (.@compass_check == 1) viewpoint 1,150,175,7,0x00FF00;
					break;
				case 8:
					mes "[Veins Guide]";
				 	mes "Если вы хотите убрать пометки на мини-карте,";
					mes "пожалуйста, скажите мне об этом.";
					set .@loop2,0;
					break;
				}
			}
			break;

		case 2:
			viewpoint 2,196,258,1,0xFF0000;
			viewpoint 2,128,266,2,0xFF00FF;
			viewpoint 2,150,175,3,0x99FFFF;
			viewpoint 2,230,161,4,0x0000FF;
			viewpoint 2,273,285,5,0x00FF00;
			viewpoint 2,150,217,6,0x00FF00;
			viewpoint 2,150,175,7,0x00FF00;
			mes "[Veins Guide]";
			mes "Хорошо, они все убраны. Если вам нужно узнать что, просто дайту мне знать.";
			mes "Желаем вам приятного пребывания в Veins.";
			break;
		case 3:
			mes "[Veins Guide]";
			mes "Когда вы используете меню ''Гид по деревне'',";
			mes "убедитесь, что место отмечено на вашей мини-карте, в правом верхнем углу экрана.";
			mes "Если вы не видите свою мини-карту, нажми комбинацию клавиш ''ctrl + tab'' или нажмите на кнопку ''Карта'' в окне Основной Информации, хорошо?";
			mes "А ещё, вы можете изменить маштаб свой мини-карты. Используй кнопку ''-'', на случай, если недо увидеть всю карту деревни.";
			break;
		case 4:
			mes "[Veins Guide]";
			mes "Желаем вам приятного пребывания в Veins.";
			close2;
			set .@loop1,0;
			break;
		}
	}
	close;
}

veins,189,101,5	duplicate(ve_guide)	Veins Guide#2	934
