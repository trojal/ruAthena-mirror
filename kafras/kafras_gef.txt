//===== eAthena Script =======================================
//= Geffen Kafras
//===== By: =========================
//= eAthena Dev Team
//===== Current Version: ===================
//= 2.5
//===== Compatible With: =====================
//= eAthena 1.0
//===== Description: ============================================
//= Description of argument settings for callfunc "F_Kafra".
//=  arg(0): When set at 0 the default Kafra message is displayed.
//=    When set to 1 the Niflhiem Kafra message is displayed.
//=    When set to 2 the Guild Kafra message is displayed. 
//=  arg(1): Set to 1 to disable teleport menu option. Otherwise set to 0.
//=  arg(2): Set to 1 to disable info menu.  Otherwise set to 0.
//=  arg(3): Cost of Storage service
//=  arg(4): Cost of Rent a Pushcart service
//===== Additional Comments: ==========================================
//= v1.1 Now using functions :)
//= v2.1b Minor changes to function calls. Using arguments.
//= This version uses arrays .[kobra_k88]
//= 2.2 New warps and prices [Evera]
//= 2.3 Can't save outside city [Evera]
//= 2.4 Updated function calls with new arguments. [L0ne_W0lf]
//=	Added unique Kafra Welcome messages.
//=	Removed North and West Kafra
//= 2.5 Fixed Kafra NPC names. [L0ne_W0lf]
//=====================================================================


// South =====================================
geffen,120,62,0	script	Сотрудник Кафра::kaf_geffen	115,{
	cutin "kafra_03",2;
	callfunc "F_KafSetGef";
	mes "[Сотрудник Кафра]";
	mes "Приветствую~!";
	mes "Корпорация Кафра";
	mes "всегда рядом с Вами.";
	mes "Так чем вам помочь?";
	callfunc "F_Kafra",5,0,0,30,750;

	M_Save:
		savepoint "geffen",119,40;
		callfunc "F_KafEnd",0,1,"в городе Geffen";
}

// East ==============================================
geffen,203,123,2	script	Сотрудник Кафра::geffen2	114,{
	cutin "kafra_04",2;
	callfunc "F_KafSetGef";
	mes "[Сотрудник Кафра]";
	mes "Добро пожаловать!";
	mes "Корпорация Кафра с её";
	mes "превосходными услугами всегда";
	mes "будет помогать путешественникам";
	mes "Рун-Мидгарда. Так что я могу";
	mes "сейчас сделать для Вас?";
	callfunc "F_Kafra",5,0,0,30,750;

	M_Save:
		savepoint "geffen",200,124;
		callfunc "F_KafEnd",0,1,"в городе Geffen";
}


// Function: Sets variables for Geffen Kafras -------------------
function	script	F_KafSetGef	{
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	setarray @wrpD$[0], "Prontera", "Al De Baran", "Orc Dungeon", "Mjolnir Dead Pit";
	setarray @wrpP[0], 1200, 1200, 1200, 1700;
	set @wrpC$[0], @wrpD$[0]+" -> "+@wrpP[0];
	set @wrpC$[1], @wrpD$[1]+" -> "+@wrpP[1];
	set @wrpC$[2], @wrpD$[2]+" -> "+@wrpP[2];
	set @wrpC$[3], @wrpD$[3]+" -> "+@wrpP[3];
	set @wrpC$[4], "Отмена";
	setarray @viewpX[0], 120, 203;
	setarray @viewpY[0], 62, 123;
	return;
}
