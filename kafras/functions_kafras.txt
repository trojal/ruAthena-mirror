//===== eAthena Script ======================================= 
//= Kafra Functions
//===== By: ================================================== 
//= eAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 6.1
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: ================================= 
//= v1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//= Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=	to block Kafra Password, read comments at 350 line
//=	Note: You can change '1337' value to another to raise password protection
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=	  Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=	Need all coords for all guides.. and somehow pass their coords to kafra.
//=	Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=	Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=	Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=	Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=		Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//===== Translated into Russian: =============================
//= Полный перевод [Vorp] vorp@yandex.ru
//============================================================ 


// Main Function ===========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables

	//Display Kafra Welcome Message
	switch(getarg(0)){
		//Default message (obsolete)
		default:
		case 0:
			mes "[Сотрудник Кафра]";
			mes "Добро пожаловать в ";
			mes "Корпорацию Кафра.";
			mes "Слуба Кафра";
			mes "всегда рядом с вами.";
			mes "Чем я могу быть полезна?";
			break;

		//Niflheim specific message
		case 1:
			mes "[Сотрудник Кафра]";
			mes "^666666Д-доброоо п-пожал-ловааать";
			mes "в К-каааффффра";
			mes "К-коорпоор-р-рациию-ууу...^000000";
			break;

		//Guild Castle Kafra message
		case 2:
			set @GID,getcharid(2);
			mes "[Сотрудник Кафра]";
	 		mes "Здравствуйте, член гильдии ^ff0000" + GetGuildName(@GID) + "^000000.";
			mes "Корпорация Кафра останется с вами везде, куда бы вы не пошли.";
			break;

		//Amatsu specific message (obsolete)
		case 3:
			mes "[Сотрудник Кафра]";
			mes "Так вы прибыли с далекой земли, чтобы изучить нашу культуру, или вы прибыли на экскурсию?";
			mes "В любом случае, почему бы вам не остаться на некоторое время?";
			mes "Воздух наполнен приятным";
			mes "ароматом полевых цветов.";
			break;

		//Louyang and Ayothaya specific message (obsolete)
		case 4:
			mes "[Сотрудник Кафра]";
			mes "С услугами Кафра";
			mes "вы всегда рядом";
			mes "с домом.";
			break;

		//NPC has it's own welcome message. (Display nothing)
		case 5:
			break;
	}
	next;

	M_Menu:
		cleararray @K_Menu0$[0],"",7;
	switch(getarg(1)){
		// Save and Storage only
		case 1:
			setarray @K_Menu0$[0],"Сохраниться","Услуги Склада","Отмена";
			break;

		// Storage only
		case 2:
			setarray @K_Menu0$[0],"Услуги Склада","Отмена";
			break;

		// No Teleport (Common)
		case 3:
			setarray @K_Menu0$[0],"Сохраниться","Услуги Склада","Прокат Телеги","Гид","Дополнительная информация","Отмена";
			break;

		// Case 4 is Einbroch no tele message.

		// No save, or teleport. (Common)
		case 5:
			setarray @K_Menu0$[0],"Услуги Склада","Прокат Телеги","Дополнительная информация","Гид","Отмена";
			break;

		// Storage and Check Other Information only.
		case 6:
			setarray @K_Menu0$[0],"Услуги Склада","Гид","Дополнительная информация","Отмена";
			break;

		// Save, Storage, and Pushcart only (Kafra Warehouse)
		case 7:
			setarray @K_Menu0$[0],"Сохраниться","Услуги Склада","Прокат Телеги","Отмена";
			break;

		// Save, Storage, Other Check information. (Turbo track)
		case 8:
			setarray @K_Menu0$[0],"Сохраниться","Услуги Склада","Дополнительная информация","Отмена";
			break;

		// Common Kafra Employee
		default:
			setarray @K_Menu0$[0],"Сохраниться","Услуги Склада","Услуги телепортации","Прокат Телеги","Гид","Дополнительная информация","Отмена";
			break;
	}

	// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
	if(getarg(0)==2) {
		cleararray @K_Menu0$[0],"",7;
		setarray @K_Menu0$[0],"Услуги Склада","Услуги Склада Гильдии","Прокат Телеги","Услуги телепортации","Отмена";
	}
	menu 	@K_Menu0$[0],K_Menu0,@K_Menu0$[1],K_Menu1,@K_Menu0$[2],K_Menu2,
		@K_Menu0$[3],K_Menu3,@K_Menu0$[4],K_Menu4,@K_Menu0$[5],K_Menu5,
		@K_Menu0$[6],K_Menu6,@K_Menu0$[7],K_Menu7;
	K_Menu0:
		set @num,0;
		goto K_Menuf;
	K_Menu1:
		set @num,1;
		goto K_Menuf;
	K_Menu2:
		set @num,2;
		goto K_Menuf;
	K_Menu3:
		set @num,3;
		goto K_Menuf;
	K_Menu4:
		set @num,4;
		goto K_Menuf;
	K_Menu5:
		set @num,5;
		goto K_Menuf;
	K_Menu6:
		set @num,6;
		goto K_Menuf;
	K_Menu7:
		set @num,7;
	K_Menuf:
		if (@K_Menu0$[@num] == "Сохраниться") return;
		if (@K_Menu0$[@num] == "Услуги Склада"){
			// Do not charge for Guild Storage
			if(getarg(0) == 2) callfunc "F_KafStor",2,0,0;
			else callfunc "F_KafStor",0,getarg(3),getarg(0);
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Услуги телепортации"){
			// Display Einbroch "No Teleport Service" notice.
			if(getarg(1) == 4){
				mes "[Сотрудник Кафра]";
				mes "Из-за ^FF0000Ограниченного";
				mes "Транспортного Соглашения^000000,";
				mes "Корпорация Кафра не может";
				mes "осуществлять услуги телепортации";
				mes "в Республике Шварцвальд.";
				next;
				mes "[Сотрудник Кафра]";
				mes "Вместо этого мы просим вас";
				mes "воспользоваться службой";
				mes "воздушных собщений. Спасибо за";
				mes "ваше понимание и сотрудничество.";
				next;
				goto M_Menu;
			}
			callfunc "F_KafTele",getarg(0);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Прокат Телеги"){
			if(callfunc("F_KafCart",getarg(0),getarg(4)) == 1) next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Гид"){
			callfunc "F_KafGuide";
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Дополнительная информация"){
			callfunc "F_KafInfo",getarg(2);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Отмена"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		}
		if (@K_Menu0$[@num] == "Услуги Склада Гильдии"){
			callfunc "F_KafStor",1,0;
			next;
			goto M_Menu;
		}

}


// Storage Function =======================================================
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if(getarg(0) == 1){
		if(guildopenstorage(0) == 1){
			mes "[Сотрудник Кафра]";
			mes "Я сожалею, но в данный момент другой член гильдии использует склад гильдии.";
			mes "Пожалуйста, дождитесь пока он не закончит.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}

	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(basicskillcheck() && getskilllv(1) < 6){
		mes "[Сотрудник Кафра]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666И-и-извиниииите,";
			mes "в-вы э-э-ээээ";
			mes "Ноовииичок... Н-нууужно";
			mes "О-основныее ууумеения у-уровень 6...^000000";
			return;
		}
		mes "Я сожалею, но вы должны";
		mes "иметь не ниже 6 уровня";
		mes "умений Новичка, если хотите";
		mes "воспользоваться складом.";
		return;
	}

	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(getarg(0) != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<getarg(1)){
				mes "[Сотрудник Кафра]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "Бо-ольше z-zeny...!";
					mes "Н-нуужно 150... zeny...";
					mes "Хррр! В-возьму кро-ооовушкой~!^000000";
					return;
				}
				//Standard Message
				mes "Я сожалею, но вы не имеете";
				mes "достаточно денег, чтобы";
				mes "воспользоваться складом. Плата";
				mes "за пользование складом "+getarg(1)+" zeny.";
				return;
			}
			set Zeny, Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + (getarg(1)/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Сотрудник Кафра]";
			mes "^666666Спасибо.. за... использование...";
			mes "Спасибо.. за... использование...";
			mes "Спасибо.. за... использование...";
			mes "Спасибо.. за... использование...";
			mes "Спасибо.. за... использование...^000000";
		}
		// Normal message
		else {
			mes "[Сотрудник Кафра]";
			mes "Сейчас, я открою";
			mes "ваш Склад.";
			mes "Спасибо за использование";
			mes "услуг Кафра.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set

	close2;
	openstorage;
	cutin "", 255;
	end;
}


// Teleport Function ==================================================
function	script	F_KafTele	{
	mes "[Сотрудник Кафра]";
	mes "Пожалуйста, выберите";
	mes "ваше направление.";
	next;

	menu 	@wrpC$[0],M_Wrp0, @wrpC$[1],M_Wrp1, @wrpC$[2],M_Wrp2, @wrpC$[3],M_Wrp3, 
		@wrpC$[4],M_Wrp4, @wrpC$[5],M_Wrp5, @wrpC$[6],M_Wrp6;

	M_Wrp0:
		set @num, 0;
		goto L_Warp;
	M_Wrp1:
		set @num, 1;
		goto L_Warp;
	M_Wrp2:
		set @num, 2;
		goto L_Warp;
	M_Wrp3:
		set @num, 3;
		goto L_Warp;
	M_Wrp4:
		set @num, 4;
		goto L_Warp;
	M_Wrp5:
		set @num, 5;
		goto L_Warp;
	M_Wrp6:
		set @num, 6;

	L_Warp:
		if (@wrpC$[@num] == "Cancel") return;
		// Consume "Free Ticket for Kafra Transportation" if available.
		// Do not consume if in Guild castle
		if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
		else {
			if (Zeny<@wrpP[@num]){
				mes "[Сотрудник Кафра]";
				mes "Я сожалею, но у вы не имеете";
				mes "достаточно денег для услуги";
				mes "Телепортации. Перемещение";
				mes "в "+@wrpD$[@num]+" стоит "+@wrpP[@num]+" zeny.";
				close2;
				cutin "", 255;
				end;
			}
			set Zeny, Zeny-@wrpP[@num];
			set RESRVPTS, RESRVPTS + (@wrpP[@num]/16);
		}
		if (@wrpD$[@num] == "Alberta") warp "alberta", 117, 56;
		if (@wrpD$[@num] == "Al De Baran") warp "aldebaran",143,110;
		if (@wrpD$[@num] == "Aldebaran") warp "aldebaran",143,110;
		if (@wrpD$[@num] == "Comodo") warp "comodo", 207, 144;
		if (@wrpD$[@num] == "Izlude") warp "izlude", 91, 105;
		if (@wrpD$[@num] == "Geffen") warp "geffen", 120, 39;
		if (@wrpD$[@num] == "Morroc") warp "morocc", 156, 46;
		if (@wrpD$[@num] == "Payon") warp "payon", 168, 103;
		if (@wrpD$[@num] == "Prontera") warp "prontera", 116, 72;
		if (@wrpD$[@num] == "Mjolnir Dead Pit") warp "mjolnir_02", 82, 347;
		if (@wrpD$[@num] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
		if (@wrpD$[@num] == "Orc Dungeon") warp "gef_fild10", 52, 326;
		if (@wrpD$[@num] == "Umbala") warp "umbala", 130, 130;
		if (@wrpD$[@num] == "Juno") warp "yuno", 157, 123;
		cutin "", 255;
		end;
}


// Cart Function ========================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(baseClass != Job_Merchant){
		mes "[Сотрудник Кафра]";
		mes "Я сожалею, но услуга";
		mes "прокат Телеги доступна";
		mes "только для Торговцев,";
		mes "Кузнецам, Оружейникам,";
		mes "Алхимикам и Биохимикам.";
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[Сотрудник Кафра]";
		mes "Но у вас уже";
		mes "есть Телега.";
		mes "К сожалению, мы не можем";
		mes "давать в аренду больше";
		mes "одной нашим клиентам.";
		return 1;
	}

	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[Сотрудник Кафра]";
		mes "Плата за аренду телеги";
		mes "составит "+getarg(1)+" Zeny. Вы действительно";
		mes "желаете взять в аренду Телегу?";
		next;
		menu "Арендовать Телегу.",-, "Отмена.",M_End;
			if(Zeny<getarg(1)){
				mes "[Сотрудник Кафра]";
				mes "Я сожалею, но у вас";
				mes "нет достаточной суммы";
				mes "zeny для оплаты Телеги,";
				mes "аренда стоит "+getarg(1)+" zeny.";
				return 1;
			}
			set Zeny,Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;

M_End:
	return 0;
}


// Pass Function ===============================================================
function	script	F_KafGuide	{
	mes "[Сотрудник Кафра]";
	mes "ОЙ...";
	return;
}

// Special Reserve Points Function ===========================================
function	script	F_KafInfo	{

	sM_Menu:
//Uncomment next line to block Kafra Storage Protection
//	if(getarg(0) == 0) menu "Узнать количество бонусных очков.",sM_ResChk, "Расположение сотрудников Кафра",sM_KafLoc, "Отмена",sM_End;
	if(getarg(0) == 0) menu "Узнать количество бонусных очков.",sM_ResChk, "Услуга защиты Склада",sM_KafCode, "Расположение сотрудников Кафра",sM_KafLoc, "Отмена",sM_End;

	sM_ResChk:
		mes "[Сотрудник Кафра]";
		mes "Посмотрим...";
		mes strcharinfo(0) + "...";
		mes "Ох, всего у вас";
		mes RESRVPTS+ " резервных бонусных очков.";
		next;
		mes "[Сотрудник Кафра]";
		mes "Вы можете обменять ваши";
		mes "резервные бонусные очки на";
		mes "награды в Главном Офисе Кафра, в Альдебаране. Пожалуйста, используй";
		mes "их для обмена на бонусные услуги или специальные награды.";
		next;
		if(getarg(0) == 1) return;
		goto sM_Menu;

	sM_KafLoc:
		viewpoint 1,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 1,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 1,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 1,@viewpX[3],@viewpY[3],4,0xFF00FF;
		next;
		viewpoint 2,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 2,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 2,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 2,@viewpX[3],@viewpY[3],4,0xFF00FF;
		goto sM_Menu;

	sM_KafCode:
		callfunc("F_SetKafCode","[Сотрудник Кафра]","Kafra Services");

	sM_End:
		return;
}


// End Function =====================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[Сотрудник Кафра]";
	//Save
	if(getarg(1)==1) {
		mes "Ваша точка востановления";
		mes "была сохранена здесь";
		mes getarg(2)+".";
		mes "Спасибо за пользование";
		mes "услугами Кафра.";
	}

	// Generic End
	else if(getarg(0)==0 || getarg(0)==5) {
		mes "Мы, Корпорация Кафра,";
		mes "всегда стремимся предложить вам наилучшие услуги. Мы надеемся, что мы соответствуем вашим приключенческим нуждам и стандартам превосходства.";
	}

	// Niflheim End
	else if(getarg(0)==1) {
		percentheal 0,- 25;
		mes "^666666Каффффра н-никогда";
		mes "не уууууууууумрёт. В...";
		mes "Всееееда с вааааами~^000000";
	}

	close2;
	cutin "", 255;
	end;
}

// Check Storage Password Function ====================
function	script	F_CheckKafCode	{
	if(#kafra_code==0) return;
	mes "Введите пароль для вашего склада:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code-getcharid(3)-1337) {
		dispbottom "Неправильный пароль.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// Set / Change / Clear Storage Password Function ====================
// getarg(0) = NPC Name, getarg(1) = Company Name
function	script	F_SetKafCode	{
	mes getarg(0);
	if(#kafra_code) {
		mes "Ваш склад защищен паролем. Что вы хотите сделать теперь?";
		next;
		menu "Изменить старый пароль -> 5000z",-,
		"Убрать пароль для склада -> 1000z",M_CLEAR,
		"Отмена",M_END;
	} else {
		mes ""+getarg(1)+" рада представить вам новую услугу:";
		mes "Защита склада с помощью дополнительного пароля.";
		next;
		menu "Новый пароль -> 5000z",M_SET,
		"Отмена",M_END;
	}

	mes getarg(0);
	mes "Пожалуйста, введите сначала ваш ^0000FF старый пароль d^000000.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0 || @code != #kafra_code-getcharid(3)-1337) {
		mes "Неверный пароль. Вы не можете установить новый пароль.";
		emotion e_hmm;
		goto M_END;
	}
	next;

M_SET:
	mes getarg(0);
	mes "Введите ^FF0000 пароль^000000, который защитит ваш склад.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "Пароль не был изменен.";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes getarg(0);
	if(Zeny < 5000) goto L_ZENY;
	set Zeny,Zeny-5000;
	//set RESRVPTS, RESRVPTS + (5000/50);	//hardcoded password doesn't add pts

	set #kafra_code,@code+getcharid(3)+1337;
	mes "Вы защитили ваш склад паролем.";
	mes "Спасибо за использование "+getarg(1)+".";
	emotion e_thx;
	goto M_END;	

M_CLEAR:
	mes getarg(0);
	mes "Пожалуйста, введите ваш пароль, что бы убрать его.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "Пароль не был удален.";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes getarg(0);
	if(Zeny < 1000) goto L_ZENY;
	set Zeny,Zeny-1000;
	//set RESRVPTS, RESRVPTS + (1000/50);	//hardcoded password doesn't add pts
	if(@code == #kafra_code-getcharid(3)-1337) {
		set #kafra_code,0;
		mes "Вы успешно убрали пароль со склада.";
		mes "Спасибо за использование "+getarg(1)+".";
		emotion e_thx;
	} else {
		mes "Неверный пароль, мы не вернём ваши 1000 Zeny.";
		mes "Пожалуйста, в следующий раз вводите верный пароль.";
		emotion e_sry;
	}
	goto M_END;	

L_ZENY:
	mes "У вас не хватает Zeny.";
	emotion e_cash;
M_END:
	close2;
	cutin "",255;
	end;
}

// Basic Password Validation Function ====================
function	script	F_EntKafCode	{
	mes "Введите число от 1000 до 10000000:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Hack: Попытка подобрать пароль.";
	}
	input @code_;
	if(@code_<1000) {
		mes "Вы не должны использовать короткий пароль.";
		return 0;
	}
	if(@code_>10000000) {
		mes "Вы не должны использовать слишком большой пароль.";
		return 0;
	}
	return @code_;
}
