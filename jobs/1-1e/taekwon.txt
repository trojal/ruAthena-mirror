//===== eAthena Script ======================================= 
//= Taekwon Job Quest
//===== By: ================================================== 
//= Tsuyuki & Samuray22
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena/Freya
//===== Description: ========================================= 
//= Official iRO TaeKwon Job Change Quest
//===== Additional Comments: ================================= 
//= 1.0 Replaced previous TK Job Quest with this one [Tsuyuki]
//= 1.1 Rewrote to the Aegis Standars. [Samuray22]
//= 1.2 Fixed position the npc is facing. [Kisuka]
//===== Translated into Russian: =============================
//= 10.12.2007 Полный перевод [Vorp]
//= 05.06.2008 Корекция перевода [Vorp]
//============================================================ 


payon,157,141,5	script	Phoenix#TKJobChange	753,{
	if(Upper == 2) {
		mes "[Phoenix]";
		mes "Здравствуй, дитя.";
		close;
	} else if(Class == Job_Taekwon) {
		mes "[Phoenix]";
		mes "Как проходит твое обучение?";
		mes "Да, твоя подготовка";
		mes "становятся всё совершенней";
		mes "и впечатляющей, но никогда";
		mes "не забывай, что ты всегда";
		mes "будешь зависеть от основ.";
		close;
	} else if(Class > Job_Novice) || (Class == Job_Novice && TK_Q == 0) {
		mes "[Phoenix]";
		mes "Наша земля. Когда-то";
		mes "прекрасный мир, был";
		mes "омрачён злом: сейчас слишком";
		mes "много людей развращенных";
		mes "тьмой, слишком много монстров,";
		mes "угрожающие невинным...";
		next;
		mes "[Phoenix]";
		mes "Хаос, который царит в этом мире,";
		mes "слишком огромен для обычных";
		mes "людей, которые не могут постоять";
		mes "за себя, против таких";
		mes "подавляющих разногласий. Однако,";
		mes "нужно стремиться их преодолевать.";
		next;
		mes "[Phoenix]";
		mes "И поэтому, я посвятил себя";
		mes "цели: стать сильнее.";
		mes "Я обучался, чтобы достичь";
		mes "просветления, развивал мастерство,";
		mes "чтобы отточить свой разум и тело,";
		mes "которыми я желал поделится с миром.";
		next;
		mes "[Phoenix]";
		mes "Я не в состоянии изменить мир";
		mes "по своему усмотрению, но я";
		mes "никогда не буду прекращать";
		mes "учиться духовно и физически.";
		mes "Я знаю, что ответ";
		mes "придёт в свое время...";
		next;
		if (Class == Job_Novice && TK_Q == 0) {
			goto L_change;
		}
		mes "[Phoenix]";
		mes "Благородный путник,";
		mes "если ты знаешь кого-то, кто";
		mes "еще не выбрал свой путь";
		mes "в жизни, пожалуйста, раскажи ему";
		mes "обо мне. Если он заинтересуется,";
		mes "я передам ему свои знания...";
		next;
L_change:

		mes "[Phoenix]";
		mes "Если вы ещё не решили, какой";
		mes "путь выбрать в своей жизни,";
		mes "я хотел бы предложить вам";
		mes "стать последователем моего";
		mes "искусства. Это будет нелегко, но";
		mes "это приведет вас к великой силе...";
		next;
		if(select("Хорошо, я присоединюсь к вам.","Нет, спасибо.") == 1) {
			if(getskilllv("NV_BASIC") < 9) {
				mes "[Phoenix]";
				mes "Какая жалость!";
				mes "Вы еще не готовы начать";
				mes "обучение под моим руководством,";
				mes "с вашим Уровнем Профессии.";
				mes "Пожалуйста, вернитесь, когда вы";
				mes "достигаете уровня 9 или выше.";
				close;
			}
			mes "[Phoenix]";
			mes "Очень хорошо... Я приму вас,";
			mes "как ученика. В начале";
			mes "обучения, ваше тело должно";
			mes "быть подготовлено для того,";
			mes "чтобы выполнить навыки,";
			mes "которые вы станете изучать.";
			next;
			if(BaseLevel > 19) {
				mes "[Phoenix]";
				mes "Хм. Я вижу, что ты";
				mes "перенес достаточные физические";
				mes "тренировки как Новичок. Очень хорошо.";
				mes "Теперь давай приготовимся к твоим";
				mes "духовным тренировкам. Раслабься";
				mes "и обратись ко мне, когда будешь готов.";
				set TK_Q, 2;
				close;
			}
			mes "[Phoenix]";
			mes "Разум не обязательно связан";
			mes "с пределами тела, но вам никогда";
			mes "не реализовать свой истинный";
			mes "потенциал, без объединения разума";
			mes "и тела. Идите, получите ^ff00001больший";
			mes "Базовый Уровень^000000, а затем возвращайтесь.";
			next;
			set TAEK_Q, BaseLevel;
			set TK_Q, 1;
			mes "[Phoenix]";
			mes "Я понимаю, что это не легкая";
			mes "задача для Новичка, но вы";
			mes "должны подготовить себя к";
			mes "трудностям в этой работе. Я буду";
			mes "ждать, что вы станете сильнее";
			mes "в следующий нашей встрече..";
			close;
		}
			mes "[Phoenix]";
			mes "Я понимаю. Жизнь может пойти";
			mes "по разным дорогам, но она одна";
			mes "и вы можете выбрать только";
			mes "единственный путь. Я надеюсь, что";
			mes "вы направитесь по просвещению, по";
			mes "вашему собственному пути, мой друг.";
			close;
	}
	switch(TK_Q) {
	case 1:
		if(BaseLevel > TAEK_Q) {
			set TK_Q, 2;
			mes "[Phoenix]";
			mes "Хорошо я ощущаю, что вы в";
			mes "большем согласии со своей";
			mes "внутренней силой. Это";
			mes "естественный результат тренировок.";
			mes "Мы готовы приступить к";
			mes "следующей части обучения";
			close;
		}
			mes "[Phoenix]";
			mes "Вы должны получить ^ff00001больше";
			mes "Базового Уровня^000000, чтобы доказать,";
			mes "что вы можете вынести трудности,";
			mes "которая влечет за собой этаработа.";
			mes "Никогда не пренебрегайте обучением.";
			close;
	case 2:
		mes "[Phoenix]";
		mes "Для вашего духовного обучения,";
		mes "я задам ряд вопросов, чтобы";
		mes "проверить ваш дух. Расслабьтесь.";
		mes "Отвечайте так честно,";
		mes "как сможете. Ваша воля и";
		mes "убеждения будут проверены.";
		next;
		mes "[Phoenix]";
		mes "Как мастер этого";
		mes "искусства, способность";
		mes "быстро принять лучшее решение";
		mes "будет необходима в бою.";
		mes "Сейчас, мы начнем опрос.";
		next;
		mes "[Phoenix]";
		mes "Когда у вас возникают";
		mes "большие трудности, как";
		mes "вы вообще реагируете?";
		next;
		switch(select( "Встречю их, с высоко поднятой головой.:Избегаю этого, так или иначе.:Соберусь и проанализирую проблему.")) {
		case 2:
			mes "[Phoenix]";
			mes "Дурак! Как вы планируете";
			mes "взрослеть, если сбегаете от";
			mes "проблем? Страх может быть";
			mes "здоровой реакцией, которая";
			mes "может спасти вашу жизнь, но";
			mes "истинная трусость презрена.";
			next;
			mes "[Phoenix]";
			mes "Я разочарован, услышав, что";
			mes "вы говорите. Никогда не";
			mes "говорите мне такого.";
			mes "Хм. Обдумайте значение";
			mes "храбрости и трусости, а затем ";
			mes "вернитесь ко мне вновь.";
			close;
		case 3:
			mes "[Phoenix]";
			mes "Собраться? Это хорошо сделать";
			mes "уже после вашей победы.";
			mes "Но лучше стоять перед проблемами,";
			mes "как только вы обнаружили их.";
			mes "Вы не всегда будете иметь";
			mes "время собраться.";
			next;
			mes "[Phoenix]";
			mes "Проблемы можно прогнозировать";
			mes "и анализировать, но я думаю,";
			mes "что отступление неразумно.";
			mes "Обдумайте свои опасения,";
			mes "а также то, что вы расцениваете";
			mes "как неудача. Затем, вернитесь.";
	close;
		case 1:
			mes "[Phoenix]";
			mes "Да, это ответ, который я";
			mes "хотел. Даже если вы не можете";
			mes "справиться с проблемой в начале,";
			mes "мы можем только извлечь выгоду,";
			mes "из такого тяжелого положения.";
			mes "Не дайте трудностям остановить себя.";
			next;
			mes "[Phoenix]";
			mes "Даже если вы терпите неудачу,";
			mes "вы все равно получите опыт";
			mes "и сможете сделать выводы.";
			mes "Нерешительность редко";
			mes "приводят к плодотворным";
			mes "результатам.";
			next;
			mes "[Phoenix]";
			mes "Вижу, вы уже осознали";
			mes "значение своей воли,";
			mes "мы перейдем к следующему";
			mes "вопросу.";
			next;
		}
		mes "[Phoenix]";
		mes "В ваших путешествиях, вам";
		mes "придется столкнуться со многими";
		mes "людьми, с разными взглядами и";
		mes "точками зрения. Неизбежно, вы";
		mes "столкнетесь с кем-то, образ";
		mes "жизни которого не сумеете понять.";
		next;
		mes "[Phoenix]";
		mes "Аналогично, этот человек";
		mes "не поймет ваш образа жизни.";
		mes "Когда ваши точки зрения";
		mes "соприкоснуться, разгорится";
		mes "конфликт, как вы поступите?";
		next;
		switch( select("Буду настаивать мою правоту:Пренебрегу противоречиям в мнениях:Приму различия и изучу их")) {
		case 1:
			mes "[Phoenix]";
			mes "Важно иметь собственное мнение.";
			mes "Однако, вы должны признать,";
			mes "что можете ошибиться, и";
			mes "противоположное мнение может";
			mes "заслуживать внимания.";
			next;
			mes "[Phoenix]";
			mes "Существует не одна правда";
			mes "и свет истины может принять";
			mes "различные оттенки. Такой";
			mes "путь природы.";
			mes "Принуждать идеи другим -";
			mes "практика тирании.";
			next;
			mes "[Phoenix]";
			mes "Не ограничивайте себя";
			mes "одной точкой зрения, не";
			mes "подавляйте свой рост,";
			mes "придерживаясь одной истины.";
			mes "Обдумайте это, а затем";
			mes "поговорите со мной снова.";
			close;
		case 2:
			mes "[Phoenix]";
			mes "Важно ладить с другими,";
			mes "но это не принесет ничего";
			mes "ценного для этого мира,";
			mes "без ваших собственных,";
			mes "уникальных, вложений";
			mes "мысли и мнений.";
			next;
			mes "[Phoenix]";
			mes "Конфликт идеалов, когда";
			mes "сходятся отношения между";
			mes "собой и другими, это";
			mes "прекрасная возможность";
			mes "расширить понимание мира";
			mes "и окружающих.";
			next;
			mes "[Phoenix]";
			mes "Размышляйте над этой идеей";
			mes "установления гармонии с собой";
			mes "и гармонией с другими.";
			mes "Затем вернитесь ко мне.";
			close;
		case 3:
			mes "[Phoenix]";
			mes "Хорошо. Вы должны видеть";
			mes "различия такими, какие";
			mes "они есть. Вы должны";
			mes "рассмотреть и критиковать";
			mes "своё собственное мнение";
			mes "с искренностью и вниманием.";
			next;
			mes "[Phoenix]";
			mes "Невозможно знать все";
			mes "в этом мире. Невозможно";
			mes "понять все точки зрения.";
			mes "Но это не означает, что ";
			mes "мнения, что вы не понимаете,";
			mes "являются недостойными.";
			next;
			mes "[Phoenix]";
			mes "Тот, с кем вы не согласны,";
			mes "может иметь ответ, который";
			mes "вы не знаете. Во время";	
			mes "вашей слабости, этот человек,";
			mes "возможно, станет величайшим";
			mes "подспорьем. Запомните это.";
			next;
		}
		mes "[Phoenix]";
		mes "Я доволен вашими ответами.";
		mes "Пожалуйста, размышляйте";
		mes "над тем, что мы обсудили";
		mes "за это время. Когда разум";
		mes "успокоиться, придите и";
		mes "поговорите со мной.";
		set TK_Q, 3;
		close;
	case 3:
		mes "[Phoenix]";
		mes "Вы чувствуете себя спокойными";
		mes "и в безопасности? Я задам";
		mes "вам очень важный вопрос.";
		mes "Дайте мне честный ответ.";	
		next;
		mes "[Phoenix]";
		mes "^ff0000Действительно ли вы готовы посвятить";
		mes "себя специальному искусству, которое";
		mes "я буду преподавать Вам, и поддерживать";
		mes "достоинство его философии?^000000";
		next;
		if( select( "Да.","Нет.") == 1) {
			mes "[Phoenix]";
			mes "Замечательно. Вы уже";
			mes "не просто ученик.";
			mes "Теперь на вас возложены";
			mes "полномочия и обязанности";
			mes "последователя ^ff0000Таеквондо^000000.";
			next;
			mes "[Phoenix]";
			mes "'Таеквон' имеет значение";
			mes "'удар кулаком и удар ногой',";
			mes "и 'До' значение 'искусство'.";
			mes "Это боевое искусство";
			mes "сосредоточено на навыках";
			mes "использования кулаков и ног.";
			next;
			mes "[Phoenix]";
			mes "Пожалуйста, используй эту";
			mes "дисциплину для оттачивания тела";
			mes "и разума, и изучай навыки,";
			mes "которые лучше всего вам подойдут.";
			mes "Никогда не уклоняйтесь от обучения,";
			mes "или вы нанесёте позор Таеквондо.";
			next;
			callfunc "Job_Change",Job_Taekwon;
			callfunc "F_ClearJobVar";
			getitem 2101,1; // Guard[0]
			mes "[Phoenix]";
			mes "Вы еще молоды, так что,";
			mes "я полагаю, вы хотите получить";
			mes "звание. Мм. В таком случае,";
			if(Sex == 0) {
				mes "теперь вы ^ff0000Девушка Таеквон^000000.";
			} else {
				mes "теперь вы ^ff0000Парень Таеквон^000000.";
			}
			mes "Да, полагаю, звучит не плохо.";
			next;
			mes "[Phoenix]";
			mes "Пожалуйста, возьми эту униформу";
			mes "и щит: хорошо используй, эти";
			mes "подарки. Когда будете учиться";
			mes "и путешествовать, просвещаете";
			mes "других о нашем искусстве, и";
			mes "изучай то, что узнаешь взамен.";
			next;
			mes "[Phoenix]";
			mes "Пришло время для вас вступить";
			mes "на свой путь, чтобы найти";
			mes "новые возможности развития";
			mes "вашей силы. Держитесь с";
			mes "гордостью, поскольку Таеквондо";
			mes "постоянная практика...";
			next;
			mes "[Phoenix]";
			mes "Отлично. Я желаю вам удачи.";
			mes "И надеюсь видеть вас когда-нибудь";
			mes "снова, "+strcharinfo(0)+".";
			close;
		}
			mes "[Phoenix]";
			mes "Гм, возможно вы ещё не";
			mes "готовы перейти от статуса";
			mes "ученика к последователю.";
			mes "Когда почувствуете себя";
			mes "готовым, придите и ";
			mes "поговорите со мной.";
			close;
	}
}

