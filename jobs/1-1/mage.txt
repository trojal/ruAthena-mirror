//===== eAthena Script ======================================= 
//= Mage Job Quest
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.7a
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Job quest for Mage classes
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Fixed input ingridients bug [Lupus]
//= v1.2 Added instant job change for High Novice [Lupus]
//= 1.3 Added Baby Class support [Lupus]
//= v1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//= 1.5 Now saves/restores all quest skills [Lupus]
//= 1.6 Merged back JFunc, CHANGED NUMBERS TO CONSTANTS [Lupus]
//= 1.7 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=	No longer uses function "F_ToHigh"
//= 1.7a Fixed a "If" in the quest. (bugreport:489) [Samuray22]
//===== Translated into Russian: =============================
//= Translated [SakhRO]
//============================================================ 

geffen_in,164,124,4	script	Mage Guildsman	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[Mage Guildsman]";
				mes "Ооо, давненько не виделись! Но разве ты не должен быть мертвым?";
				next;
				mes "[Mage Guildsman]";
				mes "Ах, видимо ты воскрес. Я рад, что ты вернулся.";
				next;
				if (getskilllv(1) < 9) {
					mes "[Mage Guildsman]";
					mes "Извини, но я думаю, что ты пока не готов обучаться магии. Почему бы тебе сначала не добить уровень Basic Skills до 9?";
					next;
					mes "[Mage Guildsman]";
					mes "Не торопись. Чем больше ты выучишь, тем больше будешь готов обучаться магии.";
					close;
				}
				mes "[Mage Guildsman]";
				mes "Хорошо, так как ты однажды уже прошел испытание, Я не буду подвергать сомнению твою квалификацию. Ты хочешь получить навыки магии немедленно, так?";
				next;
				skill 143,0,0;
				jobchange Job_Mage_High;
				skill 157,1,0;
				mes "[Mage Guildsman]";
				mes "Ухты, по какой-то причине ты стал выглядеть лучше, чем раньше. В любом случае, я верю, что ты станешь ещё сильнее";
				close;
			}
			else {
				mes "[Mage Guildsman]";
				mes "Есть ещё что-нибудь, в чем я могу помочь? Если нет, почему бы тебе не проверить свои навыки? Мир ждет тебя~!";
				close;
			}
		}
		else {
			mes "[Mage Guildsman]";
			mes "Что, тебе интересна Гильдия Магов? Я не хотел тебе этого говорить, но ты к ней не относишься.";
			next;
			mes "[Mage Guildsman]";
			mes "Я не знаю, почему ты до сих пор стоишь передо мной, Но могу сказать, что тебе не суждено быть Магом.";
			close;
		}
	}
	mes "[Mage Guildsman]";
	mes "Йоу. Что случилось?";
	next;
	switch(select("Я хочу стать Магом.:Какие требования?.:В общем то ничего.")) {
	case 1:
		mes "[Mage Guildsman]";
		if (BaseJob == Job_Mage) {
			mes "Эй, ты все ещё не понял? Ты уже Маг, тупица!";
			next;
			mes "[Mage Guildsman]";
			mes "В один из тех дней я открыл силу внутри тебя, когда ты смог создать пламя силой ума!";
			close;
		}
		if (BaseJob != Job_Novice) {
			mes "Эй, ну хватит. Хватит играть в игры. Ты не можешь быть Магом, так как у тебя уже есть другая работа.";
			close;
		}
		if (job_magician_q == 0) {
			mes "Хочешь быть Магом, да...?";
			if (sex)
				mes "Эй, посмотри на себя! Ты довольно симпатичный~! Хотя не в моем вкусе...";
			else {
				mes "Ого, какая горячая штучка~!";
				mes "Мне нравятся девочки вроде тебя~";
			}
			next;
			mes "[Mage Guildsman]";
			mes "Ладно, ты сказал, что хочешь быть магом? Хорошо, тогда подпиши заявление, что хочешь стать Магом.";
			next;
			if (select("Подписать.:Уйти.") == 1) {
				mes "[Mage Guildsman]";
				mes "Okay. Sign right there. Oh, you're very good at spelling. Alright. So your name is... " + strcharinfo(0) + ".";
				next;
				mes "[Mage Guildsman]";
				mes "Теперь настало время";
				mes "назначить тебе испытание.";
				switch(rand(3)) {
				case 1:
					mes "Приготовь ^3355FFMixed Solution No. 1^000000";
					mes "и принеси его мне.";
					set job_magician_q,1;
					break;
				case 2:
					mes "Приготовь ^3355FFMixed Solution No. 2^000000";
					mes "и принеси его мне.";
					set job_magician_q,2;
					break;
				case 3:
					mes "Приготовь ^3355FFMixed Solution No. 3^000000";
					mes "и принеси его мне.";
					set job_magician_q,3;
					break;
				default:
					mes "Приготовь ^3355FFMixed Solution No. 4^000000";
					mes "и принеси его мне.";
					set job_magician_q,4;
				}
				next;
				getitem 1092,1; //Empty_Cylinder
				mes "[Mage Guildsman]";
				mes "Ты сможешь найти необходимые ингридиенты в Guide Book внутри Гильдии. Советую тебе заглянуть в неё перед уходом.";
				next;
				mes "[Mage Guildsman]";
				mes "Как только соберешь всё необходимое, используй машину в центре комнаты, чтобы создать Mixed Solution. Удачи!";
				close;
			}
			mes "[Mage Guildsman]";
			mes "Чегооо~?! И после того, как сказал мне, что хочешь стать Магом, ты передумал?! Нужно быть более решительным!";
			close;
		}
		mes "Что? Готово...?";
		if (getskilllv(1) < 9) {
			mes "Ох, что за лентяй. Ты все ещё не подходишь по требованиям.";
			next;
			mes "[Mage Guildsman]";
			mes "Возвращайся и добей Novice Job level до 10. Также не забудь, что у тебя должен быть максимальный уровень навыка Basic Skills.";
			close;
		}
		mes "" + strcharinfo(0) + "'s испытание следующие...";
		if (job_magician_q == 1)
			mes "Создание Mixed Solution No. 1.";
		else if (job_magician_q == 2)
			mes "Создание Mixed Solution No. 2.";
		else if (job_magician_q == 3)
			mes "Создание Mixed Solution No. 3.";
		else
			mes "Создание Mixed Solution No. 4.";
		next;
		mes "[Mage Guildsman]";
		mes "Ок, давай я проверю";
		mes "правильно ли ты приготовил";
		mes "нужный тебе раствор...";
		next;
		mes "[Mage Guildsman]";
		if (countitem(1071) == 0 && countitem(1085) == 0 && countitem(1086) == 0 && countitem(1087) == 0 && countitem(1090) == 0) {
			mes "Эй, а где раствор??";
			mes "Я спрашиваю, где...? Как я смогу его проверить, если ты не дал мне его?";
			close;
		}
		else {
			if ((job_magician_q == 1 && countitem(1071) == 0) || (job_magician_q == 2 && countitem(1085) == 0) || (job_magician_q == 3 && countitem(1086) == 0) || (job_magician_q == 4 && countitem(1087) == 0)) {
				mes "Постой.";
				mes "Этот не тот раствор, ";
				mes "который я просил!";
				next;
				mes "[Mage Guildsman]";
				if (job_magician_q == 1)
					mes "Тебе нужно приготовить Mixed Solution No. 1 и принести его мне. Теперь ступай и пробуй заного.";
				else if (job_magician_q == 2)
					mes "Тебе нужно приготовить Mixed Solution No. 2 и принести его мне. Теперь ступай и пробуй заного.";
				else if (job_magician_q == 3)
					mes "Тебе нужно приготовить Mixed Solution No. 3 и принести его мне. Теперь ступай и пробуй заного.";
				else
					mes "Тебе нужно приготовить Mixed Solution No. 4 и принести его мне. Теперь ступай и пробуй заного.";

				if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
				else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
				else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
				else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
				else delitem 1090,1; //Mage_Test_Etc
				close;
			}
		}
		if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
		else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
		else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
		else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
		else delitem 1090,1; //Mage_Test_Etc
		mes "Хмм. Я вижу ты действительно старался. Для новичка это действительно круто.";
		mes "Отличная работа!";
		next;
		mes "[Mage Guildsman]";
		mes "Хорошо! Я рад сообщить тебе, что ты прошел испытание Мага. Я преобразую тебя сейчас же!";
		next;
		mes "[Mage Guildsman]";
		mes "*Гм*";
		mes "ПОЗДРАВЛЯЮ!";
		mes "Теперь ты Маг!";
		next;
		callfunc "Job_Change",Job_Mage;
		callfunc "F_ClearJobVar";
		set Zeny,Zeny+50;
		mes "[Mage Guildsman]";
		mes "'Добро пожаловать в Мой Мир~'";
		mes "Хе Хе, так хотелось сказать это. Это цитата из одного изветного фильма, если ты не знаешь~";
		next;
		mes "[Mage Guildsman]";
		mes "Отныне ты Маг, также как и мы, будем друзьями, хорошо?";
		close;
	case 2:
		mes "[Mage Guildsman]";
		mes "Хочешь быть магом, да?";
		if (sex)
			mes "Для симпотяшки вроде тебя я буду рад рассказать о требованиях!";
		else
			mes "Я буду очень рад рассказать о требованиях такой симпотичной девушке, как ты!";
		next;
		mes "[Mage Guildsman]";
		mes "Прежде всего, ты должен поднять свой Job Level до 10 и Basic Skill до 9. Затем тебе нужно будет пройти испытание Мага.";
		next;
		if (job_magician_q != 0) {
			mes "[Mage Guildsman]";
			mes "Твое задание";
			switch(job_magician_q) {
			case 1:
				mes "приготовить";
				mes "^3355FFMixed Solution No. 1^000000";
				mes "и принести его мне.";
				break;
			case 2:
				mes "приготовить";
				mes "^3355FFMixed Solution No. 2^000000";
				mes "и принести его мне.";
				break;
			case 3:
				mes "приготовить";
				mes "^3355FFMixed Solution No. 3^000000";
				mes "и принести его мне.";
				break;
			default:
				mes "приготовить";
				mes "^3355FFMixed Solution No. 4^000000";
				mes "и принести его мне.";
			}
			next;
			mes "[Mage Guildsman]";
			mes "Ты сможешь найти необходимые ингридиенты в Guide Book внутри Гильдии.";
		}
		else {
			mes "[Mage Guildsman]";
			mes "Ты сможешь узнать какой именно раствор тебе нужно будет приготовить после подписания заявления.";
		}
		next;
		mes "[Mage Guildsman]";
		mes "Дай мне знать, когда решишь стать магом, идет?";
		close;
	case 3:
		mes "[Mage Guildsman]";
		mes "Ничего...?";
		close;
	}
}

geffen_in,164,112,4	script	Mixing Machine	111,{
	mes "[Mixing Machine]";
	mes "Этот аппарат является собственностью Гильдии Магов Геффена и используется только для смешивания раствором в магических целях.";
	next;
	if (select("Использовать аппарат.:Уйти.") == 1) {
		mes "[Mixing Machine]";
		mes "Выберите";
		mes "тип";
		mes "Растворителя.";
		next;
		switch(select("Payon Solution.:Morroc Solution.:No Solvent.")) {
		case 1:
			if (countitem(1089) == 0) {
				mes "[Mixing Machine]";
				mes "Ошибка.";
				mes "Невозможно найти вещь.";
				mes "Пожалуйста, попробуйте заного.";
				mes "Прекращение работы.";
				close;
			}
			set .@mixitem2,1;
			break;
		case 2:
			if (countitem(1088) == 0) {
				mes "[Mixing Machine]";
				mes "Ошибка.";
				mes "Невозможно найти вещь.";
				mes "Пожалуйста, попробуйте заного.";
				mes "Прекращение работы.";
				close;
			}
			set .@mixitem2,2;
			break;
		case 3:
			set .@mixitem2,0;
			break;
		}
		while (1) {
			if (.@progress == 2) {
				mes "[Mixing Machine]";
				if (.@mixitem1_1 != 0) mes "Jellopy: " + .@mixitem1_1 + " ea.";
				if (.@mixitem1_2 != 0) mes "Fluff: " + .@mixitem1_2 + " ea.";
				if (.@mixitem1_3 != 0) mes "Milk: " + .@mixitem1_3 + " ea.";
				if (.@mixitem2 == 0) mes "Растворитель: не нужен.";
				if (.@mixitem2 == 1) mes "Растворитель: Payon Solution.";
				if (.@mixitem2 == 2) mes "Растворитель: Morroc Solution.";
				next;
				mes "[Mixing Machine]";
				mes "Пожалуйста, выберете: хотите ли вы начать смешивание или заного ввести количество ингридиентов.";
				next;
				switch(select("Начать смешивание.:Заного ввести ингридиенты.:Перезапуск.")) {
				case 1:
					mes "[Mixing Machine]";
					mes "Пожалуйста, поместите вещи в сосуд для смешивания. Убедитесь, что их кол-во верно.";
					next;
					mes "[Mixing Machine]";
					mes "Как только они будут помещены в Сосуд для Смешивания, вы не сможите их восстановить или вытащить.";
					next;
					mes "[Mixing Machine]";
					mes "Если все верно, нажмите кнопку 'Mix', когда будете готовы. Иначе нажмите кнопку 'Cancel'.";
					next;
					if (select("Нажать 'Mix'.:Нажать 'Cancel'.") == 1) {
						mes "[Mixing Machine]";
						mes "Помещение вещей в Смешиватель. Пожалуйста, подождите.";
						next;
						mes "[Mixing Machine]";
						if (countitem(909) < .@mixitem1_1) {
							mes "Недостаточно Jellopy.";
							mes "Пожалуйста, попробуйте заного.";
							mes "Прекращение работы.";
							close;
						}
						else if (countitem(914) < .@mixitem1_2) {
							mes "Недостаточно Fluff.";
							mes "Пожалуйста, попробуйте заного.";
							mes "Прекращение работы.";
							close;
						}
						else if (countitem(519) < .@mixitem1_3) {
							mes "Недостаточно Milk.";
							mes "Пожалуйста, попробуйте заного.";
							mes "Прекращение работы.";
							close;
						}
						else if (.@mixitem2 == 1 || .@mixitem2 == 2) {
							if (countitem(1089) == 0 && countitem(1088) == 0) {
								mes "Смешиватель не найден.";
								mes "Пожалуйста, попробуйте заного.";
								mes "Прекращение работы.";
								close;
							}
						}
						if (.@mixitem1_1 != 0) delitem 909,.@mixitem1_1; //Jellopy
						if (.@mixitem1_2 != 0) delitem 914,.@mixitem1_2; //Fluff
						if (.@mixitem1_3 != 0) delitem 519,.@mixitem1_3; //Milk
						if (.@mixitem2 == 1) delitem 1089,1; //Payon_Potion
						if (.@mixitem2 == 2) delitem 1088,1; //Morocc_Potion
						mes "Раствор готов.";
						mes "Закройте крышку.";
						set .@progress,3;
						next;
					}
					break;
				case 2:
					set .@continue,0;
					next;
					break;
				case 3:
					set .@mixitem1_1,0;
					set .@mixitem1_2,0;
					set .@mixitem1_3,0;
					set .@progress,0;
					set .@continue,0;
					mes "[Mixing Machine]";
					mes "Перезагрузка завершена.";
					mes "Начать заного?";
					next;
					if (select("Да.:Нет.") == 1)
						break;
					mes "[Mixing Machine]";
					mes "Завершение работы.";
					mes "Спасибо.";
					close;
				}
				if (.@progress == 3) break;
			}
			else if (.@progress == 1) {
				mes "[Mixing Machine]";
				mes "Ничего не найдено.";
				next;
			}
			mes "[Mixing Machine]";
			mes "Выберите ингридиенты для смешивания.";
			while(1) {
				switch(select("Jellopy.:Fluff.:Milk.:Готово к смешиванию.")) {
				case 1:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Mixing Machine]";
							mes "Ошибка: Превышен допустимый предел. Пожалуйста, введите значение меньше 10000 и попробуйте ещё раз.";
							next;
						}
						else {
							if (countitem(909) > 0) set .@mixitem1_1,.@mixitem1_1 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 2:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Mixing Machine]";
							mes "Ошибка: Превышен допустимый предел. Пожалуйста, введите значение меньше 10000 и попробуйте ещё раз.";
							next;
						}
						else {
							if (countitem(914) > 0) set .@mixitem1_2,.@mixitem1_2 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 3:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Mixing Machine]";
							mes "Ошибка: Превышен допустимый предел. Пожалуйста, введите значение меньше 10000 и попробуйте ещё раз.";
							next;
						}
						else {
						if (countitem(519) > 0) set .@mixitem1_3,.@mixitem1_3 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;

				case 4:
					if (.@progress != 2)
						set .@progress,1;
					set .@continue,1;
					next;
				}
				if (.@continue) break;
			}
		}
		mes "[Mixing Machine]";
		mes "Пожалуйста, введите ";
		mes "серийный номер";
		mes "Магического Порошка.";
		next;
		while(1) {
			input .@input;
			if (.@input < 1000 || .@input > 9999) {
				mes "[Mixing Machine]";
				if (.@input == 0) {
					mes "Вы хотите пропустить это меню?";
					next;
					if (select("Да.:Нет.") == 1) {
						break;
					}
				}
				else {
					mes "Неправильный Серийный номер.";
					mes "Попробуйте ещё раз.";
					next;
				}
			}
			else {
				mes "[Mixing Machine]";
				mes "Серийный номер #" + .@input + ", верно?";
				next;
				if (select("Подтвердить.:Отмена.") == 1) {
					if (.@input == 8472)
						set .@magic_powder,1;
					else if (.@input == 3735)
						set .@magic_powder,2;
					else if (.@input == 2750)
						set .@magic_powder,3;
					else if (.@input == 5429)
						set .@magic_powder,4;
					else
						set .@magic_powder,5;
				}
				break;
			}
		}
		mes "[Mixing Machine]";
		mes "Выберите";
		mes "Камень катализатор.";
		next;
		switch(select("Yellow Gemstone.:Red Gemstone.:Blue Gemstone.:1carat Diamond.:Skip.")) {
		case 1: set .@mixitem3,1; break;
		case 2: set .@mixitem3,2; break;
		case 3: set .@mixitem3,3; break;
		case 4: set .@mixitem3,4; break;
		case 5: break;
		}
		mes "[Mixing Machine]";
		mes "Установка.";
		mes "Инициализация";
		mes "Процесс смешивания.";
		mes "Пожалуйста, подождите.";
		next;
		mes "[Mixing Machine]";
		mes "- Пословица Дня -";
		switch(rand(1,5)) {
		case 1:
			mes "Глаз за Глаз: когда тебе достается от кого-либо, ты должен отплатить ему той же монетой.";
			break;
		case 2:
			mes "Честь превыше всего.";
			break;
		case 3:
			mes " Все причиненное зло всегда возвращается: В конечном счете, к тебе будут относится также, как ты относишься к другим.";
			mes "Другими словами 'Если ты причинишь вред кому-либо, то он поступит с тобой аткже в неудобной для тебя ситуации'.";
			break;
		case 4:
			mes "Хороший сосед лучше чем дальний родственник: Когда тебе нужна помощь, ты можешь положится на того, кто ближе к тебе.";
			break;
		default:
			mes "Скажи мне кто твой друг, и я скажу кто ты: Можно определить характер челвоека по тому, какие у него друзья.";
		}
		next;
		if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 1 && .@mixitem2 == 1 && .@mixitem3 == 1 && .@magic_powder == 1) {
			mes "[Mixing Machine]";
			mes " Solution No. 1 для теста Магов";
			getitem 1071,1; //Mage_Test_1
			next;
		}
		else if (.@mixitem1_1 == 3 && .@mixitem1_2 == 1 && .@mixitem1_3 == 1 && .@mixitem2 == 0 && .@mixitem3 == 2 && .@magic_powder == 2) {
			mes "[Mixing Machine]";
			mes "Solution No. 2 для теста Магов";
			getitem 1085,1; //Mage_Test_2
			next;
		}
		else if (.@mixitem1_1 == 6 && .@mixitem1_2 == 1 && .@mixitem1_3 == 0 && .@mixitem2 == 1 && .@mixitem3 == 3 && .@magic_powder == 3) {
			mes "[Mixing Machine]";
			mes "Solution No. 3 для теста Магов.";
			getitem 1086,1; //Mage_Test_3
			next;
		}
		else if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 0 && .@mixitem2 == 2 && .@mixitem3 == 4 && .@magic_powder == 4) {
			mes "[Mixing Machine]";
			mes "Solution No. 4 для теста Магов.";
			getitem 1087,1; //Mage_Test_4
			next;
		}
		else {
			mes "[Mixing Machine]";
			mes "Недопустимо";
			mes "Произошла Ошибка.";
			getitem 1090,1; //Mage_Test_Etc
			next;
		}
		mes "[Mixing Machine]";
		mes "Смешивание завершено.";
		mes "Спасибо.";
		close;
	}
	close;
}

pay_arche,122,100,0	script	Dollshoi	88,{
	mes "[Mage Guildsman]";
	mes "Тебе нужен Растворитель? Хмм, дай мне 50 Zeny и 1 Empty Test Tube.";
	next;
	if (select("Хорошо.:Не, забудь.") == 1) {
		mes "[Mage Guildsman]";
		if (Zeny < 50) {
			mes "Эй! У тебя недостаточно Zeny, чтобы оплатить работу.";
			close;
		}
		if (countitem(1092) == 0) {
			mes "Ты не сможешь нести растворитель без бутылки! Принеси мне Empty Test Tube.";
			close;
		}
		delitem 1092,1; //Empty_Cylinder
		set zeny,zeny-50;
		getitem 1089,1; //Payon_Potion
	}
	close;
}

moc_ruins,91,150,0	script	Ponka-Hontas	93,{
	mes "[Mage Guildsman]";
	mes "Тебе нужен Растворитель? Хмм, дай мне 50 Zeny и 1 Empty Test Tube.";
	next;
	IF (select("Хорошо.:Не, забудь.") == 1) {
		mes "[Mage Guildsman]";
		if (zENY < 50) {
			mes "Извини, но у тебя нет 50 Zeny, чтобы оплатить работу.";
			close;
		}
		if (countitem(1092) == 0) {
			mes "Ты не сможешь нести растворитель без бутылки! Принеси мне Empty Test Tube в следующий раз.";
			close;
		}
		delitem 1092,1; //Empty_Cylinder
		set zeny,zeny-50;
		getitem 1088,1; //Morocc_Potion
	}
	CLOSE;
}

geffen_in,177,112,4	script	Bookshelf	111,{
	mes "[Guide Book]";
	mes "Эта книга - собственность Гильдии Магов Геффена. Пожалуйста, относитесь к ней бережно.";
	next;
	switch(select("Solution No. 1.:Solution No. 2.:Solution No. 3.:Solution No. 4.:Закрыть.")) {
	case 1:
		mes "[Solution No. 1]";
		mes "* Список ингридиентов *";
		mes "2 Jellopy";
		mes "3 Fluff";
		mes "1 Milk";
		next;
		mes "[Solution No. 1]";
		mes "* Нужный растворитель *";
		mes "Payon Solution";
		mes "Где найти:";
		mes "Человек, в Пайоне, Гильдия Лучников, у озера.";
		next;
		mes "[Solution No. 1]";
		mes "* Серийный номер магического порошка *";
		mes "8472";
		next;
		mes "[Solution No. 1]";
		mes "* Катализатор *";
		mes "Yellow Gemstone";
		mes "(создаётся";
		mes "самой машиной)";
		close;
	case 2:
		mes "[Solution No. 2]";
		mes "* Список ингридиентов *";
		mes "3 Jellopy";
		mes "1 Fluff";
		mes "1 Milk";
		next;
		mes "[Solution No. 2]";
		mes "* Нужный растворитель *";
		mes "не используется";
		next;
		mes "[Solution No. 2]";
		mes "* Серийный номер магического порошка *";
		mes "3735";
		next;
		mes "[Solution No. 2]";
		mes "* Катализатор *";
		mes "Red Gemstone";
		mes "(создаётся";
		mes "самой машиной)";
		close;
	case 3:
		mes "[Solution No. 3]";
		mes "* Список ингридиентов *";
		mes "6 Jellopy";
		mes "1 Fluff";
		next;
		mes "[Solution No. 3]";
		mes "* Нужный растворитель *";
		mes "Payon Solution";
		mes "Где найти:";
		mes "Человек, в Пайоне, Гильдия Лучников, у озера.";
		next;
		mes "[Solution No. 3]";
		mes "* Серийный номер магического порошка *";
		mes "2750";
		next;
		mes "[Solution No. 3]";
		mes "* Катализатор *";
		mes "Blue Gemstone";
		mes "(создаётся";
		mes "самой машиной)";
		close;
	case 4:
		mes "[Solution No. 4]";
		mes "* Список ингридиентов *";
		mes "2 Jellopy";
		mes "3 Fluff";
		next;
		mes "[Solution No. 4]";
		mes "* Нужный растворитель *";
		mes "Morroc Solution";
		mes "Где найти:";
		mes "Маленькое озеро рядом с входом в пирамиду.";
		next;
		mes "[Solution No. 4]";
		mes "* Серийный номер магического порошкаe *";
		mes "5429";
		next;
		mes "[Solution No. 4]";
		mes "* Катализатор *";
		mes "1 carat Diamond";
		mes "(создаётся";
		mes "самой машиной)";
		close;
	}
}
