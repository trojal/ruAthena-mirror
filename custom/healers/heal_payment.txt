//===== eAthena Script =======================================
//= Healer Script (/w payments)
//===== By: ==================================================
//= Yor & abunch of other people
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= Any Athena Version
//===== Description: =========================================
//= NPC heals/regenerates people against zenys
//===== Additional Comments: =================================
//= alternative dog with payment.
//= each HP need 1 zeny
//= each SP need (baselevel / 5) zenys
//= calculation is done at start of the script
//= Added Duplicates And Correct payon Loc [Darkchild]
//= Some modifications. Added NPC locations. [massdriller]
//= Removed Duplicates [Silent]
//===== Translated into Russian: =============================
//= 21.01.2008 Коррекция перевода [Vorp]
//============================================================

prontera,150,184,5	script	Доктор#h2-1::Healer2	121,{
	set @tempHp, MaxHp-Hp;
	set @tempSpReal, MaxSp-Sp;
	set @tempSp, ((MaxSp-Sp) * BaseLevel) / 5;
	if (@tempHp > 0) goto WOUNDED;
	if (@tempSpReal > 0) goto ONLY_REGENERATION;

	mes "[Доктор]";
	mes "Да?";
	mes "Вы не похожи на больного человека.";
	close;

	WOUNDED:
		if (@tempSp > 0) goto WITH_REGENERATION;
		mes "[Доктор]";
		mes "О дорогоЙ, я вижу что вам нанесён вред,";
		mes "и я могу вылечить вас за: "+@tempHp+" z)";
		mes "Вы хотите, чтобы я вылечил вас?";
		next;
		menu "Вылечить ("+@tempHp+"z)",HEALINGS,"Нет, спасибо",CANCEL;

	WITH_REGENERATION:
		mes "[Доктор]";
		mes "Вы только хотите, чтобы ваши HP были излечены? ("+@tempHp+" z)";
		mes "Вы только хотите, чтобы ваши SP были излечены? ("+@tempSp+" z)?";
		mes "Или вы желаете полностю вылечится? ("+(@tempHp+@tempSp)+" z)";
		next;
		menu "Только HP ("+@tempHp+"z)",HEALINGS,"Только SP ("+@tempSp+"z)",REGENERATION,"Полностью HP и SP ("+(@tempHp+@tempSp)+"z)",HEALINGS_AND_REGEN,"Ничего, спасибо",CANCEL;

	ONLY_REGENERATION:
		mes "[Доктор]";
		mes "Итак, вы хотите востановить только SP? ("+@tempSp+" z)";
		mes "Я должен зарабатывать на жизнь...";
		next;
		menu "Востановить ("+@tempSp+"z)",REGENERATION,"Нет, спасибо",CANCEL;

	HEALINGS:
		if (Zeny < @tempHp) goto NO_ZENYS;
		set Zeny, Zeny-@tempHp;
		heal @tempHp,0;
		goto FIN;

	REGENERATION:
		if (Zeny < @tempSp) goto NO_ZENYS;
		set Zeny, Zeny-@tempSp;
		heal 0,@tempSpReal;
		goto FIN;

	HEALINGS_AND_REGEN:
		if (Zeny < (@tempHp+@tempSp)) goto NO_ZENYS;
		set Zeny, Zeny-(@tempHp+@tempSp);
		heal @tempHp,@tempSpReal;
		goto FIN;

	NO_ZENYS:
		mes "[Доктор]";
		mes "Любезный, вы же понимаете, что у вас нет достаточно денег?";
		mes "Жаль, я не смогу помочь вам.";
		close;

	FIN:
		mes "[Доктор]";
		mes "Вы полностью излечены.";
		close;

	CANCEL:
		mes "[Доктор]";
		mes "Отлично. Пожалуста, приходите сново, если вы будете нуждаться в чем-нибудь.";
		close;
}


morocc,160,97,5	duplicate(Healer2)	Доктор#h2-2	121
ayothaya,155,111,5	duplicate(Healer2)	Доктор#h2-3	121
geffen,127,70,5	duplicate(Healer2)	Доктор#h2-4	121
umbala,94,162,5	duplicate(Healer2)	Доктор#h2-5	121
payon,180,105,5	duplicate(Healer2)	Доктор#h2-6	121
alberta,185,144,5	duplicate(Healer2)	Доктор#h2-7	121
aldebaran,134,123,5	duplicate(Healer2)	Доктор#h2-8	121
izlude,125,118,5	duplicate(Healer2)	Доктор#h2-9	121
xmas,149,136,5	duplicate(Healer2)	Доктор#h2-10	121
comodo,188,162,5	duplicate(Healer2)	Доктор#h2-11	121
amatsu,200,80,5	duplicate(Healer2)	Доктор#h2-12	121
gonryun,164,130,5	duplicate(Healer2)	Доктор#h2-13	121
yuno,147,186,5	duplicate(Healer2)	Доктор#h2-14	121
niflheim,188,180,5	duplicate(Healer2)	Доктор#h2-15	121
louyang,225,103,5	duplicate(Healer2)	Доктор#h2-16	121